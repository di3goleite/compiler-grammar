"Start Symbol" = <Inicio>

! -------------------------------------------------
! CONJUNTO DE CARACTERES
! -------------------------------------------------
{Caracteres} = {Printable} - ["]

! -------------------------------------------------
! TERMINAIS
! -------------------------------------------------
IDENTIFICADOR = {Letter}({AlphaNumeric}|[_])*
NUMERO = '-'?{Number}+('.'{Number}+)?
CADEIA_CARACTERES = '"'({Caracteres})*'"'
VALORBOLEANO = 'verdadeiro' | 'falso'

! -------------------------------------------------
! PROGRAMA
! -------------------------------------------------
<Inicio> ::= 'programa' '{' <DefGlobal> '}'

<DefGlobal> ::= <DefConstante> <DefPrincipal> <DefGlobal2>
<DefGlobal2> ::= <DefMetodo> <DefGlobal2>
                |

! -------------------------------------------------
! REGRAS GERAIS
! -------------------------------------------------
<Tipo> ::= <TipoId>
         | 'vazio'
<TipoId>::= 'inteiro'
          | 'real'
          | 'texto'
          | 'boleano'

<Valor> ::= <ValorConst>
          | IDENTIFICADOR
<ValorConst> ::=  NUMERO
                 | CADEIA_CARACTERES
                 | VALORBOLEANO

<ListaParam> ::= <TipoId> IDENTIFICADOR <ListaParam2>
               |
<ListaParam2> ::= ',' <TipoId> IDENTIFICADOR <ListaParam2>
                |

<ListaArg> ::= <Valor> <ListaArg2>
<ListaArg2> ::= ',' <Valor> <ListaArg2>
              |

! -------------------------------------------------
! CONSTANTES
! -------------------------------------------------
<DefConstante> ::= 'constantes' '{' <ListaConst> '}'                       
<ListaConst> ::= <Constante> ';' <ListaConst2>                   
<ListaConst2> ::= <ListaConst>
                |             
<Constante> ::= <TipoId> <Atribuicao> <ListaAtribuicao>             
<ListaAtribuicao> ::= ',' <Atribuicao> 
                    |
<Atribuicao> ::= Identificador '=' <Valor>

! -------------------------------------------------
! PRINCIPAL
! -------------------------------------------------
<DefPrincipal> ::= 'metodo' 'principal' '('  <ListaParam> ')' ':' <Tipo> '{' <ConteudoMetodo> '}'   !Falta retorno.

! -------------------------------------------------
! METODOS
! -------------------------------------------------
<DefMetodo> ::= 'metodo' IDENTIFICADOR '(' <ListaParam> ')' ':' <Tipo> '{' <ConteudoMetodo> '}'  
                       
<ConteudoMetodo> ::= <Declaracao> 
! -------------------------------------------------
! DECLARACOES
! -------------------------------------------------
<Declaracao> ::= <DefVariavel> <Declaracao>
               | <DefSe> <Declaracao>
               | <DefEnquanto> <Declaracao>
               | <DefEscreva> ';' <Declaracao>
               | <DefLeia> ';' <Declaracao>
               | <DefChamada> <Declaracao>
               | <DefExpressao> <Declaracao>
               | Identificador <AtribuicaoVariavelMatrizVetor> <Declaracao>
               | Identificador '++' ';' 
               |Identificador '--' ';'
               | <>
               | 'Resultado' <Valor> ';' 
               


! -------------------------------------------------
! VARIAVEIS
! -------------------------------------------------
<DefVariavel> ::= 'variaveis' '{' <ListaVariaveis> '}'

<ListaVariaveis> ::= <DefinicaoVariavel> <ListaVariaveis>  | <> 

<DefinicaoVariavel> ::= <TipoId> Identificador <AtribuicaoDefiniciaoMatrizVetor>  
                       
<AtribuicaoDefiniciaoMatrizVetor> ::= <AtribuicaoVariavelMatrizVetor> | <DefinicaoMatrizVetor> 

<DefinicaoMatrizVetor> ::= '[' Numero ']' <FechamentoDefinicaoMatrizVetor>
                        
<FechamentoDefinicaoMatrizVetor>::= <DefinicaoMatrizVetor> | <AtribuicaoVariavelMatrizVetor>
                        
<AtribuicaoVariavelMatrizVetor> ::= '=' <FechamentoAtribuicaoVariavelMatrizVetor> | <FechamentoAtribuicaoVariavel> 
                                 
<FechamentoAtribuicaoVariavelMatrizVetor> ::= <Valor> <FechamentoAtribuicaoVariavel>
                                                | '{' <Valor> <ListaValoresVetor>  <FechamentoAtribuicaoVariavel>
                                                    | <DefExpressao> <FechamentoAtribuicaoVariavel>
                                                        | <DefChamada> 
                                                            | <FechamentoAtribuicaoVariavel>

<FechamentoAtribuicaoVariavel> ::=  | ',' Identificador <FechamentoDeclaracao>
                                        |';' <ListaVariaveis>

<FechamentoDeclaracao> ::= <AtribuicaoVariavelMatrizVetor> | <DefinicaoMatrizVetor> <AtribuicaoVariavelMatrizVetor>

<ListaValoresVetor> ::= ',' <Valor> <ListaValoresVetor> | ';' <Valor> <ListaValoresVetor>| ';' '}'                                               


! -------------------------------------------------
! ESTRUTURAS DE CONTROLE
! -------------------------------------------------
<DefSe> ::= 'se' '(' <Expressao> ')' 'entao' '{' <Declaracao> '}' <DefSenao> 

<DefSenao> ::= 'senao' '{' <Declaracao> '}'
             |

<DefEnquanto> ::= 'enquanto' '(' <Expressao> ')' '{' <Declaracao> '}'

!<Condicao> ::= <ExpressaoRelacional>
 !           |  '!' <ExpressaoRelacional>

! -------------------------------------------------
! COMANDOS
! -------------------------------------------------
<DefEscreva>  ::= 'escreva' '(' <ListaArg> ')'


<DefLeia>  ::= 'leia' '(' <ListaArg> ')'
            
<DefChamada> ::= IDENTIFICADOR '(' <DefChamada2>
<DefChamada2> ::= <ListaArg> ')' ';'
                  | ')' ';'

! -------------------------------------------------
! DEFINIÇÃO GERAL DA EXPRESSÃO
! -------------------------------------------------

<Expressao> ::= '(' <Expressao> ')'  
                    |  <Expressao> <OperadorLogicoBinario> <Expressao>
                        |   <DefExpressao>
                            |   '!' <Expressao>    
                                |   '!' <Termo>

<DefExpressao> ::= <ExpressaoAritmetica> | <ExpressaoLogica> | <ExpressaoRelacional> 


! -------------------------------------------------
! DEFINIÇÃO GERAL DA EXPRESSÃO RELACIONAL
! -------------------------------------------------                    

<ExpressaoRelacional> ::= <ExpressaoAritmetica> <OperadorRelacional> <Termo> 
                            | <ExpressaoAritmetica> <OperadorRelacional> <ExpressaoAritmetica>
                                |<Termo> <OperadorRelacional> <Termo>
                                    |<Termo> <OperadorRelacional> <ExpressaoAritmetica>
                                    
                           
! -------------------------------------------------
! DEFINIÇÃO GERAL DA EXPRESSÃO LÓGICA
! -------------------------------------------------                        

<ExpressaoLogica> ::= <Termo> <OperadorLogicoBinario> <Termo>   
                            

            
! -------------------------------------------------
! DEFINIÇÃO GERAL DA EXPRESSÃO ARITMÉTICA
! -------------------------------------------------

<ExpressaoAritmetica> ::= <Termo> <OperadorAritimetico> <Termo> <FechamentoExpressaoAritimetica> | '(' <ExpressaoAritmetica> ')' <FechamentoExpressaoAritimetica>

<FechamentoExpressaoAritimetica> ::= <OperadorAritimetico> <Termo> <FechamentoExpressaoAritimetica> | <>


<Termo> ::= NUMERO
           |IDENTIFICADOR

<OperadorAritimetico> ::= '+'|'-'|'*'|'/'

<OperadorLogicoBinario> ::= '&&'|'||'


<OperadorRelacional> ::= '=='
                        |'<' 
                        |'<='
                        |'>' 
                        |'>='
                        |'!=' 