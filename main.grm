"Start Symbol" = <Inicio>

! -------------------------------------------------
! CONJUNTO DE CARACTERES
! -------------------------------------------------
{Caracteres} = {Printable} - ["]

! -------------------------------------------------
! TERMINAIS
! -------------------------------------------------
IDENTIFICADOR = {Letter}({AlphaNumeric}|[_])*
NUMERO = '-'?{Number}+('.'{Number}+)?
CADEIA_CARACTERES = '"'({Caracteres})*'"'

! -------------------------------------------------
! PROGRAMA
! -------------------------------------------------
<Inicio> ::= 'programa' '{' <DefGlobal> '}'

<DefGlobal> ::= <DefConstante> <DefPrincipal> <DefGlobal2>
<DefGlobal2> ::= <DefMetodo> <DefGlobal2>
               |

! -------------------------------------------------
! REGRAS GERAIS
! -------------------------------------------------
<Tipo> ::= <TipoId>
         | 'vazio'
<TipoId>::= 'inteiro'
          | 'real'
          | 'texto'
          | 'boleano'

<Valor> ::= <ValorConst>
          | IDENTIFICADOR
<ValorConst> ::=  NUMERO
                 | CADEIA_CARACTERES

<ListaParam> ::= <TipoId> IDENTIFICADOR <ListaParam2>
               |
<ListaParam2> ::= ',' <TipoId> IDENTIFICADOR <ListaParam2>
                |

<ListaArg> ::= <Atribuicao> <ListaArg2>
<ListaArg2> ::= ',' <Atribuicao> <ListaArg2>
              |

! -------------------------------------------------
! CONSTANTES
! -------------------------------------------------
<DefConstante> ::= 'constantes' '{' <ListaConst> '}'                       
<ListaConst> ::= <Constante> ';' <ListaConst2>                   
<ListaConst2> ::= <ListaConst>
                |             
<Constante> ::= <TipoId> <AtribuicaoConst> <ListaAtribuicaoConst>             
<ListaAtribuicaoConst> ::= ',' <AtribuicaoConst> 
                    |
<AtribuicaoConst> ::= IDENTIFICADOR '=' <Valor>

! -------------------------------------------------
! PRINCIPAL
! -------------------------------------------------
<DefPrincipal> ::= 'metodo' 'principal' '('  <ListaParam> ')' ':' <Tipo> '{' <Declaracao> '}'

! -------------------------------------------------
! METODOS
! -------------------------------------------------
<DefMetodo> ::= 'metodo' IDENTIFICADOR '(' <ListaParam> ')' ':' <Tipo> '{' <Declaracao> '}'  

! -------------------------------------------------
! DECLARACOES
! -------------------------------------------------
<Declaracao> ::= <DefVariavel> <Declaracao>
               | <DefSe> <Declaracao>
               | <DefEnquanto> <Declaracao>
               | <DefEscreva> <Declaracao>
               | <DefLeia> <Declaracao>
               | <DefChamada> <Declaracao>
               | <DefExpressao> <Declaracao>
               | <DefResultado>
               |

! -------------------------------------------------
! VARIAVEIS
! -------------------------------------------------
<DefVariavel> ::= 'variaveis' '{' <ListaVar> '}'
<ListaVar> ::= <DeclaracaoVar> <ListaVar2>
<ListaVar2> ::= <DeclaracaoVar> <ListaVar2>
                    |
<DeclaracaoVar> ::= <TipoId> <ListaDeclaracaoVar> ';'
<ListaDeclaracaoVar> ::= <AtribuicaoVar> <ListaDeclaracaoVar2>
<ListaDeclaracaoVar2> ::= ',' <AtribuicaoVar> <ListaDeclaracaoVar2>
                        |
<AtribuicaoVar> ::= <Declarador> <AtribuicaoVar2>
<AtribuicaoVar2> ::= '=' <Inicializacao>
                      |
<Inicializacao> ::= <Atribuicao>
                  | '{' <ListaInicializacaoVar> <Inicializacao2>
<Inicializacao2> ::= '}'
        | ',' '}'
<ListaInicializacaoVar> ::= <Inicializacao> <ListaInicializacaoVar2>
<ListaInicializacaoVar2> ::= ',' <Inicializacao> <ListaInicializacaoVar2>
                           |
<Declarador> ::= IDENTIFICADOR <Declarador2>
<Declarador2> ::= '[' <Declarador3>
                |
<Declarador3> ::= <ExprAtribuicao> ']' <Declarador2>
                | ']' <Declarador2>

! -------------------------------------------------
! ESTRUTURAS DE CONTROLE
! -------------------------------------------------
<DefSe> ::= 'se' '(' <ExprRelacionalIgual> ')' 'entao' '{' <Declaracao> '}' <DefSenao> 

<DefSenao> ::= 'senao' '{' <Declaracao> '}'
             |

<DefEnquanto> ::= 'enquanto' '(' <ExprRelacionalIgual> ')' '{' <Declaracao> '}'

! -------------------------------------------------
! COMANDOS
! -------------------------------------------------
<DefEscreva>  ::= 'escreva' '(' <ListaArg> ')' ';'


<DefLeia>  ::= 'leia' '(' <ListaArg> ')' ';'
            
<DefChamada> ::= IDENTIFICADOR '(' <DefChamada2>
<DefChamada2> ::= <ListaArg> ')' ';'
                  | ')' ';'

<DefResultado> ::= 'resultado' <Expressao> ';'

! -------------------------------------------------
! EXPRESSOES
! -------------------------------------------------                 
<DefExpressao> ::= ';'
             	 | <Expressao> ';'

<Expressao> ::= <Atribuicao> <Expressao2>
<Expressao2> ::= ',' <Atribuicao> <Expressao2>
               |

<Atribuicao> ::= <ExprAtribuicao> <Atribuicao2>
<Atribuicao2> ::= '=' <ExprAtribuicao> <Atribuicao2>
                |
<ExprAtribuicao> ::= <ExprLogicaOu>
<ExprLogicaOu> ::= <ExprLogicaE> <ExprLogicaOu2>
<ExprLogicaOu2> ::= '||' <ExprLogicaE> <ExprLogicaOu2>
                  |
<ExprLogicaE> ::= <ExprRelacionalIgual> <ExprLogicaE2>
<ExprLogicaE2> ::= '&&' <ExprRelacionalIgual> <ExprLogicaE2>
                 |
<ExprRelacionalIgual> ::= <ExprRelacionalOutras> <ExprRelacionalIgual2>
<ExprRelacionalIgual2> ::= <OpRelacionalIgual> <ExprRelacionalOutras> <ExprRelacionalIgual2>
                         |
<ExprRelacionalOutras> ::= <ExprAritmeticaAd> <ExprRelacionalOutras2>
<ExprRelacionalOutras2> ::= <OpRelacionalOutros> <ExprAritmeticaAd> <ExprRelacionalOutras2>
                          |
<ExprAritmeticaAd> ::= <ExprAritmeticaMult> <ExprAritmeticaAd2>
<ExprAritmeticaAd2> ::= <OpAritmeticoAd> <ExprAritmeticaMult> <ExprAritmeticaAd2>
                      |
<ExprAritmeticaMult> ::= <ExprUnaria> <ExprAritmeticaMult2>
<ExprAritmeticaMult2> ::= <OpAritmeticoMult> <ExprUnaria> <ExprAritmeticaMult2>
                        |
<ExprUnaria> ::= <OpUnario> <ExprUnaria>
              | <ExprPosfixa>
<ExprPosfixa> ::= <Fator> <ExprPosfixa2>
<ExprPosfixa2> ::= <OpPosfixo> <ExprPosfixa2>
                 |

<OpRelacionalIgual> ::= '==' | '!='

<OpRelacionalOutros> ::= '<' | '>' | '<=' | '>='

<OpAritmeticoAd> ::= '+' | '-'

<OpAritmeticoMult> ::= '*' | '/'

<OpUnario> ::= '++' | '--' | '!'

<OpPosfixo> ::= '++' | '--' | '[' <Expressao> ']' | '(' <OpPosfixo2> | '.' IDENTIFICADOR
<OpPosfixo2> ::= ')'
               | <ListaArg> ')'

<Fator> ::= IDENTIFICADOR
          | NUMERO
          | CADEIA_CARACTERES
          | 'true'
          | 'false'
          | '(' <Expressao> ')'